/*
 * Copyright (c) Rye Client 2024-2025.
 *
 * This file belongs to Rye Client,
 * an open-source Fabric injection client.
 * Rye GitHub: https://github.com/RyeClient/rye-v1.git
 *
 * THIS PROJECT DOES NOT HAVE A WARRANTY.
 *
 * Rye (and subsequently, its files) are all licensed under the MIT License.
 * Rye should have come with a copy of the MIT License.
 * If it did not, you may obtain a copy here:
 * MIT License: https://opensource.org/license/mit
 *
 */

import org.gradle.internal.os.OperatingSystem

class Lwjgl {
	static String getClassifier() {
		switch (OperatingSystem.current()) {
			case OperatingSystem.LINUX:
				return "linux"
			case OperatingSystem.MAC_OS:
				return System.getProperty("os.arch").startsWith("aarch64") ? "macos-arm64" : "macos"
			case OperatingSystem.WINDOWS:
				return System.getProperty("os.arch").contains("64") ? "windows" : "windows-x86"
			default:
				throw new Error("Unrecognized or unsupported Operating system. Please set \"lwjglNatives\" manually")
		}
	}
}

plugins {
	id 'fabric-loom' version '1.10-SNAPSHOT'
	id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm'
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	mavenCentral()
}

loom {
	splitEnvironmentSourceSets()

	mods {
		"rye" {
			sourceSet sourceSets.main
			sourceSet sourceSets.client
		}
	}

}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// Use Log4j2 which is compatible with Minecraft/Fabric
	implementation 'org.apache.logging.log4j:log4j-slf4j2-impl:2.20.0'

	// Jackson for JSON processing
	implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.2'
	implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

	// LWJGL HarfBuzz bindings
	include(implementation("org.lwjgl:lwjgl-harfbuzz:3.3.3"))

	// Platform-specific natives (gradle is cool)
	def lwjglNatives = Lwjgl.getClassifier()
	include(implementation("org.lwjgl:lwjgl-harfbuzz:3.3.3:natives-${lwjglNatives}"))

	// i do not want to make a renderer
	modImplementation "io.github.0x3c50.renderer:renderer-fabric:2.1.0"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": inputs.properties.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

}

jar {
	inputs.property "archivesName", project.base.archivesName

	from("LICENSE") {
		rename { "${it}_${inputs.properties.archivesName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.archives_base_name
			from components.java
		}
	}
}
